; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "FMU-API"
#define MyAppVersion "10.0"
#define MyAppPublisher "Retail-Automation LTD, Krasnoyarsk, 2025"
#define MyAppURL "https://www.au124.ru"
#define MyAppExeName "fmu-api.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{18108806-2223-414A-AB56-03AB266B3028}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\Automation\{#MyAppName}
DefaultGroupName=fmu-api
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=D:\DesignSharpC\FmuApi\FMU-API\setup
OutputBaseFilename=setup-x64
SetupIconFile=D:\DesignSharpC\FmuApi\builds\favicon.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
PrivilegesRequired=admin
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64
;InfoAfterFile=info_after.txt

[Tasks]
Name: "installcouchdb"; Description: "Install CouchDB"; GroupDescription: "Additional components:"; Flags: unchecked

[Run]
Filename: "{app}\{#MyAppExeName}"; Parameters: "--register"; Flags: runhidden; Description: "Installing FMU-API service"
Filename: "sc.exe"; Parameters: "start fmu-api"; Flags: runhidden; Description: "running FMU-API service"
Filename: "{app}\couchdb\install.cmd"; Flags: runhidden; Description: "Installing couchdb service"; Tasks: installcouchdb;

[UninstallRun]
Filename: "{app}\couchdb\uninstall.cmd"; Flags: runhidden; Check: CouchDBWasInstalled;
Filename: "sc.exe"; Parameters: "stop fmu-api"; Flags: runhidden;
Filename: "{app}\Automation\{#MyAppName}\{#MyAppExeName}"; Parameters: "--unregister"; Flags: runhidden
Filename: "sc.exe"; Parameters: "delete fmu-api"; Flags: runhidden;

[Messages]
FinishedLabel=Установка программы [name] выполнена успешно.

[Code]
function InitializeSetup(): Boolean;
var
  ResultCode: Integer;
begin
  Result := True;
  
  if Exec('sc.exe', 'query "FMU-API"', '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
  begin
    if ResultCode = 0 then
    begin
      Log('FMU-API service found, stopping...');
      Exec('sc.exe', 'stop "FMU-API"', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
      Sleep(3000);
    end;
  end;
end;

function CouchDBWasInstalled: Boolean;
begin
  Result := FileExists(ExpandConstant('{app}\couchdb\uninstall.cmd'));
end;

function UpdateReadyMemo(Space, NewLine, MemoUserInfoInfo, MemoDirInfo, MemoTypeInfo,
  MemoComponentsInfo, MemoGroupInfo, MemoTasksInfo: String): String;
begin
  Result := '';
  if WizardIsTaskSelected('installcouchdb') then
  begin
    Result := Result + 'После установки будут доступны следующие параметры CouchDB:' + NewLine;
    Result := Result + Space + 'Адрес: http://localhost:6984' + #13#10;
    Result := Result + Space + 'Логин: admin' + #13#10;
    Result := Result + Space + 'Пароль: admin' + #13#10;
  end;
  
  Result := Result + MemoDirInfo + NewLine + NewLine;
  
  if MemoTasksInfo <> '' then
    Result := Result + MemoTasksInfo + NewLine + NewLine;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    if WizardIsTaskSelected('installcouchdb') then
    begin
      MsgBox('Параметры подключения к CouchDB:' + #13#13 +
             'Адрес: http://localhost:6984' + #13#10 +
             'Логин: admin' + #13#10 +
             'Пароль: admin', mbInformation, MB_OK);
    end;
  end;
end;

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Files]
Source: "D:\DesignSharpC\FmuApi\builds\x64 full\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\DesignSharpC\FmuApi\builds\wwwroot\*"; DestDir: "{app}\wwwroot"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "D:\DesignSharpC\FmuApi\builds\CouchDB\*"; DestDir: "{app}\couchdb"; Flags: ignoreversion recursesubdirs createallsubdirs; Tasks: installcouchdb
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

;[Icons]
;Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"

